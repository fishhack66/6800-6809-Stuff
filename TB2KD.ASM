* 2018/01/24
* STACK		$1F47 : $A047
* SUBSTK	$1F80 : $A080
*
ACIACS	EQU	$8018
RAMSSS	EQU	$0980
RAMEEE	EQU	$1EFF
*
	NAM	FIRST
	ORG	$1A
V	RMB	2
W	RMB	2
Z	RMB	2
RND	RMB	2
LN	RMB	2
PX	RMB	2
SP	RMB	2
LNADRS	RMB	2
BUFEND	RMB	2
LDFLG	RMB	1
AFLG	RMB	1
CHCNT	RMB	1
F	RMB	1
BUFFER	RMB	80
EXSTK	EQU	*
EXSP	RMB	2
	RMB	52
PRSTRT	RMB	2
PREND	RMB	2
RAMEND	RMB	2
SUBSP	RMB	2
FORSP	RMB	2
	RMB	64
FORSTK	EQU	*
STACK	EQU	$1F47	; $A047
SUBSTK	EQU	$1F80	; $A080
INEEE	EQU	$E1AC
OUTEEE	EQU	$E1D1
RESET	EQU	$E0D0
	ORG	$100
*
	JSR	START
	JMP	CONTRL
*
LOADM	BSR	IN
	CMP A	#2
	BNE	LOADM
	BRA	GL2
*
GTLINE	LDA A	LDFLG
	BNE	LOADM
	LDA A	AFLG
	BEQ	GL1
	LDA A	#10
	CLR B
	LDX	#LN
	JSR	ADD3
	JSR	PNUM
	JSR	OUTS
	BRA	GL3
*
GL1	LDA A	#'#
	JSR	OUT
GL2	LDX	#BUFFER
GL3	EOR A	RND
	STA A	RND
	BSR	IN
	CMP A	#$D
	BNE	GL4
	CLR	0,X
	STX	BUFEND
	LDA A	LDFLG
	BNE	*+5
NEWL2	JSR	NWLINE
	LDX	#BUFFER
	RTS
*
GL4	CMP A	#$18
	BNE	GL5
	BSR	NEWL2
	BRA	GL3
*
GL5	CMP A	#$F
	BNE	GL6
	LDA A	#$5F
	JSR	OUT
	CPX	#BUFFER
	BEQ	GL3
	DEX
	BRA	GL3
*
GL6	CPX	#BUFFER+70
	BEQ	ERR100
	STA A	0,X
	INX
	BRA	GL3
*
ERR100	LDA B	#100
	JMP	ERRMSG
*
IN	JSR	INFFF
	AND A	#$7F
	BEQ	IN
	CMP A	#$7F
	BEQ	IN
	CMP A	#3
	BNE	BR6
	BRA	CONTRL
*
BREAK	PSH A
*---------------------
*	LDA A	$8004
*	BMI	BR5
*
	JSR	POLCAA
	BCC	BR5
*---------------------
	BSR	IN
BR5	PUL A
BR6	RTS
*
INIT1	LDX	PRSTRT
	TPA
	STA A	0,X
	INX
	STA A	0,X
	STX	PREND
	RTS
*
LOAD	BSR	INIT1
	STA A	LDFLG
	BRA	C4
*
AUTO	TPA
	STA A	AFLG
	JSR	EXPRPP
	SUB A	#10
	SBC B	#0
SETLN	STA A	LN+1
	STA B	LN
	RTS
*
STOP	JSR	PRINT
	JSR	PSX
	BRA	C3
*
START	LDX	#RAMSSS
	STX	PRSTRT
*---------------------
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
	NOP
*
*	LDA A	#$AA
*MEMTST	INX
*	LDA B	1,X
*	STA A	1,X
*	LDA A	1,x
*	STA B	1,X
*	CMP A	#$AA
*
*	BEQ	MEMTST
*
	LDX	#RAMEEE
*----------------------
	STX	RAMEND
NEW	BSR	INIT1
CONTRL	LDS	#STACK
	SEI
	BSR	INIT2
C3	JSR	NWLINE
	LDX	#RDYMSG
	JSR	PD
	TPA
	STA A	LNADRS
C4	LDS	#STACK
	JSR	GTLINE
	JSR	GETN
	BCC	DIRECT
	BSR	SETLN
	BSR	EDIT
	BRA	C4
*
EDIT	STX	PX
	STS	SP
	BSR	LDTST
	STX	Z
	BCS	INSERT
	BSR	NXTL
	INX
	TXS
	LDX	Z
MOVE1	ASL B
	PUL B
	STA B	0,X
	INX
	BPL	MOVE1
	BCC	MOVE1
	DEX
	STX	PREND
INSERT	LDX	PX
	LDA A	0,X
	BEQ	INS3
	LDA A	BUFEND+1
	SUB A	PX+1
	ADD A	#3
	LDX	PREND
	ADD A	PREND+1
	STA A	PREND+1
	BCC	*+5
	INC	PREND
	BSR	SIZE
	BCC	INS2
	STX	PREND
ERR110	LDA B	#110
	BRA	ERROR
*
LDTST	LDA A	LDFLG
	BEQ	LNFD
	LDX	PREND
	DEX
	SEC
	RTS
*
INS2	LDS	PREND
	INX
MOVE2	DEX
	LDA A	0,X
	PSH A
	CPX	Z
	BNE	MOVE2
	LDS	LN
	STS	0,X
	INX
	LDS	PX
	DES
MOVE3	INX
	PUL A
	STA A	0,X
	BNE	MOVE3
INS3	LDS	SP
	RTS
*
INIT2	LDX	#0
	STX	LDFLG
	LDX	#FORSTK
	STX	FORSP
	LDX	#SUBSTK
	STX	SUBSP
	LDX	#EXSTK
	STX	EXSP
	RTS
*
DIRECT	STX	PX
	LDX	#TABLE2-2
	BRA	STMT4
*
NXTL	INX
	INX
NXTL2	TST	0,X
	BNE	*-3
	RTS
*
FINDER	LDX	LNADRS
	BMI	LNFD
	BSR	LNFD+2
	BCC	LNFD3+4
LNFD	LDX	PRSTRT
	LDA A	LN+1
	LDA B	LN
	BMI	ERR120
	BNE	LNFD3
	TST A
	BNE	LNFD3
ERR120	LDA B	#120
ERROR	JMP	ERRMSG
*
	BSR	NXTL
	INX
LNFD3	BSR	CMPX
	BHI	*-5
	RTS
*
IF	JSR	EXPRPP
	TST B
	BNE	STMT3
	TST A
	BEQ	NXTL2
	BRA	STMT3
*
SIZE	LDA A	RAMEND+1
	LDA B	RAMEND
	SUB A	PREND+1
	SBC B	PREND
	RTS
*
C7	JMP	C3
*
RUN	BSR	INIT2
	LDX	PRSTRT
STMT	STX	LNADRS
	LDA A	0,X
	ASL A	,
	LDA A	LNADRS
	BLS	C7
	INX
	INX
STMT3	JSR	BREAK
	STX	PX
	LDX	#TABLE-2
STMT4	LDS	#STACK
	JSR	OPRATE
	DEX
STMT5	LDA A	0,X
	INX
	CMP A	#':
	BEQ	STMT3
	TST A
	BNE	STMT5
	BRA	STMT
*
HEXA	CLR B
	STA B	Z
	BSR	TSTHEX
	BCC	SIZE
HEX2	ROL A
	ASL A
	ASL A
	ASL A
	ASL A
SHIFT	ROL	Z
	ROL B
	ASL A
	BNE	SHIFT
	INX
	BSR	TSTHEX
	BCS	HEX2
	LDA A	Z
	RTS
*
TSTHEX	JSR	TSTN
	BCS	HEX3
	SUB A	#7
	CMP A	#$3A
	CLC
	BLT	*+4
	CMP A	#$40
HEX3	RTS
*
CMPX	CMP B	0,X
	BNE	*+4
	CMP A	1,X
	RTS
*
SRCH2	STX	PX
	LDX	#TABLE4-2
SEARCH	STS	SP
S0	LDS	PX
	DES
	INX
S1	INX
S2	PUL A
	CMP A	#$20
	BEQ	S2
	TAB
	SUB A	0,X
	ASL A
	BNE	S5
	BCC	S1
	INX
S4	INS
S3	STS	PX
	LDS	SP
	RTS
*
S5	LDA A	0,X
	INX
	BPL	S5
	CMP B	#'.
	BEQ	S4
	COM A
	BNE	S0
	BRA	S3
*
PRINT	BSR	SKB9
	BEQ	PR9
PR3	BSR	PQUOTE
	BEQ	PR4
	STX	PX
	LDX	#TABLE5-2
	JSR	OPRATE
PR4	BSR	SKB9
	BEQ	PR9
	CMP A	#';
	BEQ	PR5
	CMP A	#',
	BNE	ERR130
	BSR	PT
PR5	INX
PR6	BSR	SKB9
	BNE	PR3
	RTS
*
SKB9	JMP	SKB
*
PR9	JMP	NWLINE
*
	JSR	OUTS
PT	LDA A	CHCNT
	BIT A	#7
	BNE	PT-3
	RTS
*
CHR	BSR	FACTPP
OUT7	JMP	OUT
*
TAB	BSR	FACTPP
	TAB
	BRA	TAB2
*
	JSR	OUTS
TAB2	CMP B	CHCNT
	BHI	*-5
	RTS
*
PQUOTE	BSR	SKB9
	CMP A	#'"
	BEQ	*+6
	CMP A	#''
	BNE	PQ5
	TAB
	INX
	BRA	*+4
*
PQ4	BSR	OUT7
	LDA A	0,X
	BEQ	ERR130
	INX
	CBA
	BNE	PQ4
PQ5	RTS
*
FACTPP	JSR	FACT9
	JMP	POP
*
ERR130	LDA B	#130
	BRA	*+4
*
ERR140	LDA B	#140
	JMP	ERRMSG
*
DIVIDE	LDA A	0,X
	ORA A	1,X
	BEQ	ERR140
	BSR	SETCNT
	BSR	ROL2X
	ROL A
	NEG A
	TAB
	BSR	CHECK
	ROL A
	EOR A	#1
	ROR A
	BSR	DIV3
	JSR	CMPX
	BNE	RDIV
	CLR A
	TAB
	INC	3,X
	BNE	*+4
	INC	2,X
RDIV	INX
	INX
	RTS
*
BINDIV	BSR	SETCNT
DIV2	BSR	ROL2X
	ROL A
	ROL B
	SUB A	1,X
	SBC B	0,X
	BSR	CHECK
DIV3	DEC	F
	BNE	DIV2
ROL2X	ROL	3,X
	ROL	2,X
	RTS
*
CHECK	PSH B
	EOR B	0,X
	PUL B
	SEC
	BPL	*+7
	ADD A	1,X
	ADC B	0,X
	CLC
	RTS
*
SETCNT	LDA A	#16
	STA A	F
	CLR A
	TAB
	RTS
*
MULT	BSR	SETCNT
MLT2	ASL A
	ROL B
	ROL	1,X
	ROL	0,X
	BCC	*+6
	ADD A	3,X
	ADC B	2,X
	DEC	F
	BNE	MLT2
	INX
	INX
	JMP	STAX
*
PNUM	LDX	0,X
	STX	W
	LDX	#W
	BSR	ABSX
	BCC	PN2
	LDA A	#'-
	BSR	OUT
PN2	DEX
	DEX
	LDA B	#10
	STA B	1,X
	CLR	0,X
	ASR B
PN3	PSH B
	BSR	BINDIV
	PUL B
	PSH A
	DEC B
	BNE	PN3
	LDX	#BUFFER
	LDA B	#4
PN4	LDA A	F
	PUL A
	BNE	PN5
	STA A	F
	BEQ	*+4
PN5	BSR	PN6
	DEC B
	BNE	PN4
	PUL A
PN6	ADD A	#$30
	TST	AFLG
	BEQ	*+5
	STA A	0,X
	INX
OUT	INC	CHCNT
	JSR	BREAK
	JMP	OUTEEE
*
ABSX	TST	0,X
	BPL	RABS
NEGX	NEG	1,X
	BNE	*+4
	DEC	0,X
	COM	0,X
RABS	RTS
*
SKB2	INX
SKB	LDA A	0,X
	CMP A	#$20
	BEQ	SKB2
	TST A
	BEQ	*+5
	CMP A	#':
	SEC
	RTS
*
EXPR3	BSR	SKB
	CMP A	#'-
	BNE	E3
	BSR	E9
	BRA	E5
*
E3	CMP A	#'+
	BEQ	E4
	DEX
E4	BSR	TERM2
E5	LDX	PX
	BSR	SKB
	CMP A	#'+
	BNE	E6
	BSR	TERM2
E55	BSR	ADD
	BRA	E5
*
E6	CMP A	#'-
	BNE	E7
	BSR	E9
	BRA	E55
*
E9	BSR	TERM2
	BRA	NEGX
*
ADD	JSR	POP
	INX
	INX
ADD3	ADD A	1,X
	ADC B	0,X
STAX	STA A	1,X
	STA B	0,X
E7	RTS
*
ABS	JSR	FACT9
	BSR	SAVE
	BSR	ABSX
	LDX	PX
	RTS
*
TSTEQ	BSR	SKB
	CMP A	#'=
	BNE	R5
	INS
	INS
	INC B
	BRA	R3
*
REL	CLR B
	BSR	TSTEQ
	BSR	R7
	BNE	R6
	BSR	TSTEQ
R2	CMP A	#'>
	BNE	R5
R3	INC B

R4	INX
	CLR A
R5	RTS
*
R6	BSR	R2
	BNE	R5
	BSR	TSTEQ
R7	CMP A	#'<
	BNE	R5
	ADD B	#4
	BRA	R4
*
TERM2	BSR	TERM
SAVE	STX	PX
	LDX	EXSP
	RTS
*
EXPR	BSR	EXPR3
	BSR	REL
	TST B
	BEQ	R5
	PSH B
	BSR	EXPR3
	JSR	POP
	LDX	EXSP
	SUB A	1,X
	SBC B	0,X
	PUL B
	BLT	LT
	BGT	GT
	TST A
	BEQ	EQ
GT	ASR B
EQ	ASR B
LT	AND B	#1
	STA B	1,X
	CLR	0,X
	LDX	Z
	RTS
*
GETN	BSR	TSTN
	BCC	NOTN
	CLR B
	STA B	Z+1
GN2	STA B	Z
	AND A	#$F
	STA A	F
	LDA A	Z+1
	ASL A
	ROL B
	ASL A
	ROL B
	ADD A	Z+1
	ADC B	Z
	ASL A
	ROL B
	ADD A	F
	STA A	Z+1
	ADC B	#0
	INX
	BSR	TSTN
	BCS	GN2
	LDA A	Z+1
	SEC
NOTN	RTS
*
TSTN	JSR	SKB8
	CMP A	#'0
	CLC
	BLT	*+4
	CMP A	#$3A
T3	RTS
*
TERM	BSR	FACT
T1	BSR	SKB8
	CMP A	#'*
	BNE	T2
	BSR	FACT2
	JSR	MULT
T11	STX	EXSP
	LDX	PX
	BRA	T1
*
T2	CMP A	#'/
	BNE	T3
	BSR	FACT2
	JSR	DIVIDE
	BRA	T11
*
FACT2	BSR	FACT
	JMP	SAVE
*
HEXA2	INX
	JSR	HEXA
	BRA	PUSH
*
PEEK	BSR	FACT
	JSR	POP
	BSR	TAX
	LDA A	0,X
	CLR B
	BRA	PUSH-2
*
FACT	INX
	BSR	GETN
	BCS	PUSH
	CMP A	#'#
	BEQ	PEEK
	CMP A	#'$
	BEQ	HEXA2
	STX	PX
	LDX	#TABLE3
OPRATE	JSR	SEARCH
	LDA A	1,X
	PSH A
	LDA A	0,X
	PSH A
	LDX	PX
	RTS
*
FVAR	BSR	TAX
	LDA A	1,X
	LDA B	0,X
	LDX	PX
PUSH	STX	Z
	LDX	EXSP
	DEX
	DEX
	CPX	BUFEND
	BLE	ERR150
PUSH2	JSR	STAX
PUSH3	STX	EXSP
	LDX	Z
	RTS
*
ERR150	LDA B	#150
ER	JMP	ERRMSG
*
ERR160	LDA B	#160
	BRA	ER
*
ERR170	LDA B	#170
	BRA	ER
*
SKB8	JMP	SKB
*
NOTFNC	BSR	TSTV
	BCC	FVAR
	LDA A	0,X
	CMP A	#'(
	BNE	ERR160
	INX
FACT9	JSR	EXPR
	BSR	SKB8
	CMP A	#')
	BNE	ERR160
	INX
	RTS
*
TAX	STX	PX
	STA A	Z+1
	STA B	Z
	LDx	Z
	RTS
*
MOD	JSR	EXPR
	LDA A	0,X
	CMP A	#',
	BNE	ERR170
	INX
MOD2	BSR	FACT9
	STX	Z
	LDX	EXSP
	JSR	DIVIDE
	BRA	PUSH2
*
RANDOM	LDA A	RND+1
	LDA B	RND
	ADD B	RND+1
	ADC A	#$95
	ADC B	#$AB
	STA B	RND+1
	STA A	RND
	BSR	PUSH
	BRA	MOD2
*
USER	BSR	USER2
	BRA	PUSH-2
*
USER2	BSR	ARG2
	PSH A
	PSH B
	BSR	ARG
	PSH A
	PSH B
	BSR	ARG
	PSH A
	PSH B
	TPA
	PSH A
	BSR	FACT9+3
	STX	PX
	RTI
*
ARG	LDA A	0,X
	CMP A	#',
	BNE	NONE
	INX
ARG2	JMP	EXPRPP
*
NONE	CLR A
	CLR B
	RTS
*
TSTV	BSR	SKB8
	CMP A	#$40
	BEQ	ARRAY
	CMP A	#'A
	BCS	NOTV
	CMP A	#'Z
	BHI	NOTV
	ASL A
	CLR B
	INX
	RTS
*
ARRAY	JSR	FACT
	JSR	POP
	BIT B	#$C0
	BNE	ERR11
	SEC
	ROL A
	ROL B
	ADD A	PREND+1
	ADC B	PREND
	CMP B	RAMEND
	BNE	*+4
	CMP A	RAMEND*1
	BCC	ERR11
	CLC
	RTS
*
ERR11	JMP	ERR110
*
NOTV	SEC
	RTS
*
TSTEQ2	JSR	TSTEQ
ERR180	LDA B	#180
ERR19	JMP	ERRMSG
*
LET3	JSR	TSTV
	BCS	ERR180
LET4	STA A	W+1
	STA B	W
	BSR	TSTEQ2
	BSR	EXPRPP
	STX	PX
	LDX	W
	RTS
*
ERR190	LDA B	#190
	BRA	ERR19
*
THEN	CLR B
	PSH B
	JSR	GETN
	BCS	GOTO2
	JMP	STMT3
*
POKE	JSR	FACT
	BSR	POP
	BSR	LET4
	TAB
	BRA	LET2
*
PSX	STS	SP
	TXS
	LDX	SUBSP
	CPX	#STACK+3
	BEQ	ERR190
	DEX
	DEX
	STS	0,X
RESTR	STX	SUBSP
	TSX
	LDS	SP
	RTS
*
PLX	LDX	SUBSP
	CPX	#SUBSTK
	BEQ	ERR190
	STS	SP
	LDS	0,X
	INX
	INX
	BRA	RESTR
*
EXPRPP	BSR	EXPR7
POP	STX	Z
	LDX	EXSP
	CPX	#EXSTK
	BEQ	ERR180
	LDA A	1,X
	LDA B	0,X
	INX
	INX
	STX	EXSP
	LDX	Z
	RTS
EXPR9	LDX	PX
EXPR7	JMP	EXPR
*
GOTO	CLR A
GOSUB	PSH A
	BSR	EXPRPP
GOTO2	JSR	SETLN
	PUL A
	TST A
	BEQ	*+4
	BSR	PSX
	JSR	FINDER
	BCC	*+6
ERR200	LDA B	#200
	BRA	ERR22
*
	JMP	STMT
*
LET	JSR	SKB
	CMP A	#'#
	BEQ	POKE
LET1	JSR	LET3
	STA A	1,X
LET2	STA B	0,X
	LDX	PX
	RTS
*
FOR	BSR	LET1
	LDA A	W+1
	LDA B	W
	JSR	PUSH
	JSR	SRCH2
	CPX	#TOMSG
	BNE	ERR210
	BSR	EXPR9
	JSR	SRCH2
	CPX	#STPMSG
	BEQ	FOR1
	LDA A	#1
	CLR B
	JSR	PUSH-2
	BRA	*+4
*
FOR1	BSR	EXPR9
	STX	PX
	LDA A	PX+1
	LDA B	PX
	JSR	PUSH
	LDA A	W+1
	LDA B	W
	LDX	FORSP
FOR3	BSR	TST2
	BCC	FOR4
	LDX	FORSP
	CPX	#FORSTK-64
	BNE	FOR4+2
ERR210	LDA B	#210
ERR22	JMP	ERRMSG
*
FOR4	BSR	INX8
	BSR	FORPSH
	BRA	NEXT5
*
TSTSTK	JSR	CMPX
	BEQ	TST3
	BSR	INX8
TST2	CPX	#FORSTK
	BNE	TSTSTK
	SEC
TST3	RTS
*
FORPSH	BSR	*+2
	BSR	*+2
	JSR	POP
	DEX
	DEX
STAX2	JMP	STAX
ERR220	LDA B	#220
	BRA	ERR22
*
NEXT	JSR	TSTV
	STX	PX
	LDX	FORSP
	BCC	*+6
	LDA A	1,X
	LDA B	0,X
	BSR	TST2
	BCS	ERR220
	STX	FORSP
	LDA A	5,X
	LDA B	4,X
	LDX	0,X
	JSR	ADD3
	LDX	FORSP
	CMP B	2,X
	SEC
	BLT	NEXT4
	CLC
	BGT	NEXT4
	CMP A	3,X
	BEQ	AGAIN
NEXT4	ROR A
	EOR A	4,X
	BMI	AGAIN
	BSR	INX8
NEXT5	STX	FORSP
	LDX	PX
	RTS
*
INX8	BSR	*+2
	INX
	INX
	INX
	INX
	RTS
*
AGAIN	LDX	6,X
	RTS
*
INPUT	STX	W
	LDA A	W
	BEQ	ERR10
	LDX	BUFEND
	STX	V
	LDX	W
	JSR	PQUOTE
	BNE	IN2
	BSR	TSTCM
IN2	JSR	TSTV
	BCS	ERR10
	PSH A
	PSH B
	STX	W
	LDX	V
	BSR	SKB1
	BNE	IN3
	LDA A	#'?
	BSR	OUT3
	BSR	OUTS
	JSR	GL2
IN3	JSR	EXPRPP
	BSR	TSTCM
	STX	V
	TSX
	LDX	0,X
	INS
	INS
	JSR	STAX
	LDX	W
	BSR	SKB1
	BEQ	IN4
	CMP A	#',
	BNE	ERR10
	INX
	BRA	IN2
*
ERR10	JMP	ERR100
*
TSTCM	BSR	TSTCM2
	BNE	*+3
	INX
IN4	RTS
*
TSTCM2	PSH A
	BSR	SKB1
	CMP A	#',
	PUL A
	RTS
*
SKB1	JMP	SKB
*
OUTS	LDA A	#$20
OUT3	JMP	OUT
*
LIST	CLR B
	LDA A	#1
	JSR	SETLN
	LDA B	#$7F
	LDA A	#$FF
	PSH A
	BSR	SKB1
	PUL A
	BEQ	LIST2
	JSR	EXPRPP
	JSR	SETLN
	BSR	TSTCM2
	BNE	LIST2
	INX
	JSR	EXPRPP
LIST2	STA A	SP+1
	STA B	SP
	LDX	SP
	BNE	*+7
	LDX	#32767
	STX	SP
	JSR	LNFD
	BRA	*+4
*
LIST3	BSR	LISTX
	LDA A	SP+1
	LDA B	SP
	JSR	CMPX
	BCC	LIST3
	LDA A	#3
	BSR	OUT3
C9	JMP	CONTRL
*
LISTX	LDA A	#2
	BSR	OUT3
	BSR	PNUM9
	INX
	LDA A	#$20
	BSR	PD2
NWLINE	STX	Z
	LDX	#CRLF
	BSR	PD
	CLR	CHCNT
LX2	LDX	Z
	RTS
*
PNUM9	STX	Z
	JSR	PNUM
	BRA	LX2
*
PD2	BSR	OUT3
	INX
PD	LDA A	0,X
	BNE	PD2
	INX
	RTS
*
ERRMSG	LDS	#STACK
	BSR	NWLINE
	LDX	#ERR
	BSR	PD
	TBA
	CLR B
	BSR	PRACC
	LDX	LNADRS
	BMI	C9
	BSR	NWLINE
	BSR	LISTX
	BRA	C9
*
PREXPR	JSR	EXPRPP
PRACC	PSH A	e
	PSH B
	STX	Z
	TSX
	BSR	PNUM9+2
	PUL B
	PUL A
	RTS
*
N	EQU	$80
TABLE2	EQU	*
	FCB	'LIS
	FCB	N+'T
	FDB	LIST
	FCB	'LOA
	FCB	N+'D
	FDB	LOAD
	FCB	'RU
	FCB	N+'N
	FDB	RUN
	FCB	'E
	FCB	N+'X
	FDB	RESET
	FCB	'AUT
	FCB	N+'O
	FDB	AUTO
	FCB	'NE
	FCB	N+'W
	FDB	NEW
TABLE	EQU	*
	FCB	'LE
	FCB	N+'T
	FDB	LET
	FCB	'I
	FCB	N+'F
	FDB	IF
	FCB	'GOT
	FCB	N+'O
	FDB	GOTO
	FCB	'GOSU
	FCB	N+'B
	FDB	GOSUB
	FCB	'RE
	FCB	N+'T
	FDB	PLX
	FCB	'NEX
	FCB	N+'T
	FDB	NEXT
	FCB	'FO
	FCB	N+'R
	FDB	FOR
	FCB	'THE
	FCB	N+'N
	FDB	THEN
	FCB	'RE
	FCB	N+'M
	FDB	NXTL2
	FCB	'PRIN
	FCB	N+'T
	FDB	PRINT
	FCB	'INPU
	FCB	N+'T
	FDB	INPUT
	FCB	'P
	FCB	N+'R
	FDB	PRINT
	FCB	'I
	FCB	N+'N
	FDB	INPUT
	FCB	'STO
	FCB	N+'P
	FDB	STOP
	FCB	'EN
	FCB	N+'D
	FDB	CONTRL
	FCB	$FF
	FDB	LET
*
TABLE3	EQU	*
	FCB	'RND
	FCB	N+'(
	FDB	RANDOM
	FCB	'ABS
	FCB	N+'(
	FDB	ABS
	FCB	'MOD
	FCB	N+'(
	FDB	MOD
	FCB	'USR
	FCB	N+'(
	FDB	USER
	FCB	$FF
	FDB	NOTFNC
*
TABLE4	EQU	*
	FCB	'T
	FCB	N+'O
TOMSG	FDB	$1234
	FCB	'STE
	FCB	N+'P
STPMSG	FDB	$ABCD
*
TABLE5	EQU	*
	FCB	'CHR
	FCB	N+'(
	FDB	CHR
	FCB	'TAB
	FCB	N+'(
	FDB	TAB
	FCB	$FF
	FDB	PREXPR
*
ERR	FCC	/ERROR /
	FCB	0
RDYMSG	FCC	/READY/
*----------------------
*CRLF       FCB      $0D,$0A,$FF,$FF,$FF,$FF,$FF,0
*
CRLF	FCB	$0D,$0A,0
*
POLCAA	LDAA	ACIACS
	ASRA
	RTS
*----------------------
INFFF	JSR	INEEE
	CMP A	#$60
	BCS	*+4
	SUB A	#$20
	CMP A	#$25
	BNE	*+4
	LDA A	#$40
	RTS
*
	END
